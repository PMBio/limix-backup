import distutils.sysconfig
import numpy
import os
#get all the build variables we need
Import('env','mymode','external_include','limix_include','limix','nlopt')
localenv = env.Clone()

print "Python site-packages directory: %s" % distutils.sysconfig.get_python_lib()

#detect python and numpy include directories
python_lib_path = distutils.sysconfig.get_python_lib()
python_inc_path = distutils.sysconfig.get_python_inc()
numpy_inc_path = numpy.get_include()

#figure out target install path
variant_dir = os.path.join('#',mymode)


#add python path and numpy path
localenv.Append(CPPPATH = [python_inc_path,numpy_inc_path])
localenv.Append(SWIGFLAGS=['-python','-c++'])
localenv.Append(SWIGPATH =[limix_include,external_include])
localenv.Append(LIBS=['liblimix.a','libnlopt.a','z'])
localenv.Append(LIBPATH=[python_lib_path,os.path.join(variant_dir,'limix'),os.path.join(variant_dir,'nlopt')])

#dynamic lookups, needed with MAC at least
if(localenv['PLATFORM']=='darwin'):
	localenv.Append(LINKFLAGS = ['-undefined', 'dynamic_lookup'])

#build from .i file
srclst = ['limix.i']
localenv.Depends(srclst, [nlopt,limix])


#lib=localenv.SharedLibrary('_core.so',srclst)
lib=localenv.LoadableModule('_core.so',srclst)

#copy files
target_dir = os.path.join(variant_dir,"interfaces","python")
#Command("limix","limix",[Copy(os.path.join(target_dir,"limix"),"limix")])

Return('lib')
